/**
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

allprojects {
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = '10.11.0'
        configDirectory = file("$rootProject.projectDir/config/checkstyle")
    }

    group = 'com.github.camel-tooling'
    version = '1.0.2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url "https://repository.apache.org/snapshots/"
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    sourceCompatibility = '17'

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    test {
        afterTest { desc, result ->
            logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        jvmArgs = [
                // avoid JBUIScale "Must be precomputed" error, because IDE is not started (LoadingState.APP_STARTED.isOccurred is false)
                '-Djava.awt.headless=true'
        ]
    }
}
